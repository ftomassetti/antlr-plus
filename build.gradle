buildscript {
   ext.kotlin_version = '1.0.3'

   repositories {
     mavenCentral()
     maven {
        name 'JFrog OSS snapshot repo'
        url  'https://oss.jfrog.org/oss-snapshot-local/'
     }
     jcenter()
   }

   dependencies {
     classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
   }    
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'antlr'
apply plugin: 'maven-publish'

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

group = 'me.tomassetti'
version = '0.2'

dependencies {
    antlr "org.antlr:antlr4:4.5.1"
    compile "org.antlr:antlr4-runtime:4.5.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile 'junit:junit:4.12'
    testCompile group: 'com.google.guava', name: 'guava', version: '19.0'
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'me.tomassetti.antlrplus.antlrparser']
    outputDirectory = new File("generated-src/antlr/main/me/tomassetti/antlrplus/antlrparser".toString())
}
compileKotlin.dependsOn generateGrammarSource
sourceSets {
    antlrdeps {
        kotlin.srcDir 'src/main/antlr-deps/'
    }
    generated {
        kotlin.srcDir 'generated-src/antlr/main/'
    }
}
compileKotlin.source sourceSets.antlrdeps.kotlin, sourceSets.generated.kotlin, sourceSets.main.kotlin

generateTestGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'me.tomassetti.antlrplus']
    outputDirectory = new File("generated-src/antlr/test/me/tomassetti/antlrplus".toString())
}
compileTestKotlin.dependsOn generateTestGrammarSource
sourceSets {
    test_generated {
        java.srcDir 'generated-src/antlr/test/'
    }
}
compileTestJava.source sourceSets.test_generated.java
compileTestKotlin.source sourceSets.test.kotlin, sourceSets.test_generated.java

clean{
    delete "generated-src"
}

idea {
    module {
        sourceDirs += file("generated-src/antlr/main")
        sourceDirs += file("src/main/antlr-deps")
        testSourceDirs += file("generated-src/antlr/test")
    }
}
